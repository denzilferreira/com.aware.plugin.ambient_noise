apply plugin: 'com.android.library'

android {
    compileSdkVersion compile_sdk
    buildToolsVersion build_tools

    defaultConfig {
        minSdkVersion minimum_sdk
        targetSdkVersion target_sdk
        versionCode version_code
        versionName version_readable

        resValue "string", "provider_ambient_noise", "com.aware.plugin.ambient_noise.provider.ambient_noise"
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }

    sourceSets {
        main {
            java.srcDirs += '../com.aware.plugin.ambient_noise/src/main/java'
            res.srcDirs += '../com.aware.plugin.ambient_noise/src/main/res'
            manifest.srcFile '../com.aware.plugin.ambient_noise/src/main/AndroidManifest.xml'
        }
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    maven { url "https://jitpack.io" } //MPAndroidChart
}

dependencies {
    api "com.github.denzilferreira:aware-client:$aware_libs"
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0-alpha'
    implementation 'me.franfernandez:fftpack:0.1'
}

